apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka'
// Add the coverage generating task `jacocoTestReport`
// See: https://github.com/codecov/example-android/tree/4a81cecfe873a6f9ebfcfad52f185fe38998e587
apply plugin: 'jacoco-android'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "0.1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDirs += sharedTestDir
        }
        androidTest {
            java.srcDirs += sharedTestDir
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokka"
}

// Configure jacoco settings
// See https://github.com/arturdm/jacoco-android-gradle-plugin/tree/dd9d8ceaa9e7c21babddf048a9d02ab36b489bd6
jacoco {
    toolVersion = "0.8.4"
}
// Lookup test for Robolectric using
tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    def robolectric_version = '4.3.1'
    testImplementation "org.robolectric:robolectric:$robolectric_version"

    def security_version = '1.0.0-rc01'
    implementation "androidx.security:security-crypto:$security_version"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'junit:junit:4.12'

    def android_test_version = '1.1.1'
    testImplementation "androidx.test.ext:junit:$android_test_version"
    androidTestImplementation "androidx.test.ext:junit:$android_test_version"

    def truth_version = '1.0'
    testImplementation "com.google.truth:truth:$truth_version"
    androidTestImplementation "com.google.truth:truth:$truth_version"

    def mockk_version = '1.9.3'
    testImplementation "io.mockk:mockk:$mockk_version"
    androidTestImplementation "io.mockk:mockk-android:$mockk_version"
}
